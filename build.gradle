buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:6.4.1"
        classpath 'com.github.johnrengelman.shadow:com.github.johnrengelman.shadow.gradle.plugin:7.1.2'
    }
}

apply plugin: 'java-library'

configure(subprojects) {
    group = 'com.traveltime'
    version = project.findProperty('tag') ?: 'v0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            url "https://maven.restlet.org/"
        }
    }

    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    def imageName = "${project.version.toString().toLowerCase()}-test"

    task("dockerBuildTest", type: Exec, dependsOn: [project.shadowJar]) {
        environment "DOCKER_BUILDKIT", "1"
        commandLine "docker", "build", ".", "-f", "src/test/resources/Dockerfile", "-t", imageName
    }

    task("dockerTest", type: Exec, dependsOn: "dockerBuildTest") {
        environment "IMAGE_NAME", imageName
        commandLine "bash", "src/test/resources/run-tests.sh"
    }

    dependencies {
        implementation 'com.traveltime:traveltime-sdk-java:1.1.17'
        implementation 'io.vavr:vavr:0.10.4'
        implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.6'
    }

    ext.buildPlugin = { project, solrVersion ->
        project.version = project.findProperty('tag') ?: "v0.2-SNAPSHOT"

        project.dependencies {
            compileOnly "org.apache.solr:solr-core:${solrVersion}"
        }
    }
}

