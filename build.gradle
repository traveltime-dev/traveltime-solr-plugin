buildscript {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:6.4.1"
        classpath 'com.github.johnrengelman.shadow:com.github.johnrengelman.shadow.gradle.plugin:7.1.2'
        classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
    }
}

apply plugin: 'java-library'

def common = project(':common')

configure(subprojects - common) {
    group = 'com.traveltime'
    version = project.findProperty('tag') ?: 'v0.1-SNAPSHOT'

    repositories {
        mavenCentral()
        maven {
            url "http://maven.restlet.talend.com/"
            allowInsecureProtocol = true
        }
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }

    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'
    apply plugin: "com.diffplug.spotless"

    spotless {
        java {
            googleJavaFormat('1.23.0').reflowLongStrings()
            formatAnnotations()
        }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    def imageName = "${project.version.toString().toLowerCase()}-test"

    task("dockerBuildTest", type: Exec, dependsOn: [project.shadowJar]) {
        environment "DOCKER_BUILDKIT", "1"
        commandLine "docker", "build", ".", "-f", "src/test/resources/Dockerfile", "-t", imageName
    }

    task("dockerTest", type: Exec, dependsOn: "dockerBuildTest") {
        environment "IMAGE_NAME", imageName
        commandLine "bash", "src/test/resources/run-tests.sh"
    }

    dependencies {
        implementation common
        implementation 'com.traveltime:traveltime-sdk-java:2.0.0'
        implementation 'io.vavr:vavr:0.10.4'
        implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.6'
        implementation 'com.google.guava:guava:25.1-jre'
    }

    ext.buildPlugin = { project, solrVersion ->
        project.version = project.findProperty('tag') ?: "v0.2-SNAPSHOT"

        project.dependencies {
            compileOnly "org.apache.solr:solr-core:${solrVersion}"
        }
    }
}

